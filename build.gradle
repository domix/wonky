plugins {
  id("groovy")
  id("jacoco")
  id("com.github.johnrengelman.shadow") version "7.1.2"
  id("io.micronaut.application") version "3.3.2"
  id 'com.github.ben-manes.versions' version '0.42.0'
  id 'com.google.cloud.tools.jib' version '3.2.1'
}

version = "0.5.0"
group = "com.example"

repositories {
  mavenCentral()
}

dependencies {
  annotationProcessor("org.projectlombok:lombok")
  annotationProcessor("io.micronaut:micronaut-http-validation")
  annotationProcessor("io.micronaut.security:micronaut-security-annotations")
  implementation("io.micronaut:micronaut-http-client")
  implementation("io.micronaut:micronaut-jackson-databind")
  implementation("io.micronaut:micronaut-runtime")
  implementation("io.micronaut.cache:micronaut-cache-caffeine")
  implementation("io.micronaut.security:micronaut-security")
  implementation("jakarta.annotation:jakarta.annotation-api")
  compileOnly("org.projectlombok:lombok")
  runtimeOnly("ch.qos.logback:logback-classic")
  implementation("io.micronaut:micronaut-validation")


  implementation 'commons-io:commons-io:2.11.0'
  testImplementation 'org.apache.commons:commons-lang3:3.12.0'
}

application {
  mainClass.set("wonky.Application")
}

java {
  sourceCompatibility = JavaVersion.toVersion("17")
  targetCompatibility = JavaVersion.toVersion("17")
}

graalvmNative.toolchainDetection = false

micronaut {
  runtime("netty")
  testRuntime("spock2")
  processing {
    incremental(true)
    annotations("wonky.*")
  }
}

test {
  testLogging {
    events "passed", "skipped", "failed"
    exceptionFormat "full"
  }
  finalizedBy jacocoTestReport
}

jacocoTestReport {
  dependsOn test
  //We are running on Continuos Integration environment
  def ci = System.getenv('CI') == "true"
  reports {
    xml.required = ci
    html.required = !ci
  }
}

jacoco {
  toolVersion = "0.8.8"
}

jib {
  from {
    image = 'amazoncorretto@sha256:5b35afdfbce6f11dcb5a0df84c5cc6d3f1765e5cb8ce54c43c6d7c1d12f42490'
    //sha256 for amazoncorretto:17-alpine3.15-full
  }
  to {
    image = "domix/wonky:${ project.version }".toLowerCase()
    auth {
      username = System.getenv('DOCKER_BUILDER_USERNAME') ?: ''
      password = System.getenv('DOCKER_BUILDER_PASSWORD') ?: ''
    }
  }
  container {
    ports = ['8080']
    volumes = ['/etc/wonky/tenants.yaml']
  }
}
