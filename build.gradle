buildscript {
  repositories {
    mavenCentral()
    mavenLocal()
    jcenter()
    maven { url "https://plugins.gradle.org/m2/" }
  }
  dependencies {
    classpath 'com.bmuschko:gradle-docker-plugin:4.9.0'
    classpath "com.github.ben-manes:gradle-versions-plugin:0.21.0"
  }
}

plugins {
  //id "com.github.hierynomus.license" version "0.14.0"
  id 'net.researchgate.release' version '2.8.0'
  //id 'com.github.kt3k.coveralls' version '2.8.2'

  id "io.spring.dependency-management" version "1.0.7.RELEASE"
  id "com.github.johnrengelman.shadow" version "5.0.0"
  id "net.ltgt.apt-eclipse" version "0.21"
  id "net.ltgt.apt-idea" version "0.21"
  id "com.github.hierynomus.license" version "0.15.0"
}

group "wonky"

apply plugin: "application"
apply plugin: 'groovy'
apply plugin: 'jacoco'
apply plugin: com.bmuschko.gradle.docker.DockerRemoteApiPlugin
apply plugin: "com.github.ben-manes.versions"

ext {
  email = 'domingo.suarez@gmail.com'
  lombokVersion = '1.18.8'
  groovyVersion = '2.5.7'
}

repositories {
  mavenLocal()
  mavenCentral()
  jcenter()
}

dependencyManagement {
  imports {
    mavenBom 'io.micronaut:micronaut-bom:1.1.3'
  }
}

configurations {
  // for dependencies that are needed for development only
  developmentOnly 
}

dependencies {
  //WARN: It's very important to preserve this order (https://github.com/micronaut-projects/micronaut-core/issues/218#issuecomment-392549294)
  compileOnly "org.projectlombok:lombok:${lombokVersion}"
  annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
  annotationProcessor "io.micronaut:micronaut-graal"
  annotationProcessor "io.micronaut:micronaut-inject-java"
  annotationProcessor "io.micronaut:micronaut-validation"
  compile "io.micronaut:micronaut-http-client"
  compile "io.micronaut:micronaut-http-server-netty"
  compile "io.micronaut:micronaut-inject"
  compile "io.micronaut:micronaut-validation"
  compile "io.micronaut:micronaut-runtime"
  compile "io.micronaut:micronaut-security"
  compile "io.micronaut:micronaut-management"
  //compile "org.telegram:telegrambots:4.1.2"
  //compile "io.micronaut:micronaut-tracing"
  //compile 'io.jaegertracing:jaeger-core:0.33.1'
  compile 'commons-io:commons-io:2.6'
  compile 'com.github.ben-manes.caffeine:caffeine:2.7.0'
  compileOnly "io.micronaut:micronaut-inject-java"
  compileOnly "org.codehaus.groovy:groovy-all:${groovyVersion}"
  compileOnly "com.oracle.substratevm:svm"

  /*
  compile 'com.kohlschutter.junixsocket:junixsocket-common:2.2.0'
  compile 'com.kohlschutter.junixsocket:junixsocket-native-common:2.2.0'
  compile 'com.kohlschutter.junixsocket:junixsocket-native:2.2.0'
  */

  runtime "ch.qos.logback:logback-classic:1.2.3"
  runtime "io.micronaut:micronaut-graal"
  //compile 'javax.xml.bind:jaxb-api:2.3.0'
  //runtime "io.jaegertracing:jaeger-thrift"
  testAnnotationProcessor "io.micronaut:micronaut-inject-java"
  testCompile "org.junit.jupiter:junit-jupiter-api"
  testCompile "io.micronaut.test:micronaut-test-junit5"
  testRuntime "org.junit.jupiter:junit-jupiter-engine"
  
  testCompile 'org.apache.commons:commons-lang3:3.9'
  testCompile "io.micronaut:micronaut-inject-java"
  testCompile "org.codehaus.groovy:groovy-all:${groovyVersion}"
  testCompile "org.spockframework:spock-core:1.3-RC1-groovy-2.5", {
    exclude module: 'groovy-all'
  }
  testCompile 'net.bytebuddy:byte-buddy:1.9.13'
  testCompile 'org.objenesis:objenesis:3.0.1'
}

shadowJar {
  mergeServiceFiles()
}

test.classpath += configurations.developmentOnly

mainClassName = "wonky.Application"
compileJava.options.compilerArgs += '-parameters'
compileTestJava.options.compilerArgs += '-parameters'

jacocoTestReport.dependsOn check

jacocoTestReport {
  //We are running on Continuos Integration environment
  def ci = System.getenv('CI') == "true"
  reports {
    xml.enabled ci
    html.enabled !ci
  }
}

run.classpath += configurations.developmentOnly
test.classpath += configurations.developmentOnly
run.jvmArgs('-noverify', '-XX:TieredStopAtLevel=1')

mainClassName = "wonky.Application"

tasks.withType(JavaCompile){
  options.encoding = "UTF-8"
  options.compilerArgs.add('-parameters')
}

task createDockerfileNative(type: com.bmuschko.gradle.docker.tasks.image.Dockerfile) {
  destFile = project.file('build/libs/DockerfileNative')
  from 'oracle/graalvm-ce:1.0.0-rc15', 'graalvm'
  def ci = System.getenv('CI') == "true"
  def extra = ''
  if(ci) {
    extra = ' (CI builded)'
  }
  copyFile "wonky-${project.version}-all.jar", '/opt/wonky/wonky.jar'
  workingDir '/opt/wonky'
  runCommand 'native-image --no-server -cp wonky.jar'

  from 'frolvlad/alpine-glibc'
  label(['maintainer': "Domingo Suarez Torres${ extra } <${ email }>"])
  exposePort 8080
  copyFile '/opt/wonky/wonky', '/opt/wonky/wonky', 'graalvm'
  entryPoint '/opt/wonky/wonky'
}

task buildImageNative(type: com.bmuschko.gradle.docker.tasks.image.DockerBuildImage) {
  dependsOn createDockerfileNative
  inputDir = createDockerfileNative.destFile.get().asFile.parentFile
  dockerFile = createDockerfileNative.destFile.get().asFile
  tags = ["domix/wonky:${project.version}-native".toLowerCase()]
}

task pushImageNative(type: com.bmuschko.gradle.docker.tasks.image.DockerPushImage) {
  dependsOn buildImageNative
  def im = buildImageNative.tags.get().first()
  imageName = im
  def ci = System.getenv('CI') == "true"
  def enabledPush = true
  if(ci && project.version.toString().toLowerCase().endsWith("snapshot")) {
    enabledPush = false
  }
  enabled = enabledPush
  println "Push enabled native: ${enabled}"
}

task createDockerfile(type: com.bmuschko.gradle.docker.tasks.image.Dockerfile) {
  destFile = project.file('build/libs/DockerfileJre')

  //from "adoptopenjdk/openjdk11:jdk-${jreVersion}_7-slim"
  //from "openjdk:${jreVersion}-jre-slim"
  //from "adoptopenjdk/openjdk11:jre-${jreVersion}_7-alpine"
  from "adoptopenjdk/openjdk11-openj9:jre-${jreVersion}_7_openj9-0.14.0-alpine"
  def ci = System.getenv('CI') == "true"
  def extra = ''
  if(ci) {
    extra = ' (CI builded)'
  }
  label(['maintainer': "Domingo Suarez Torres${ extra } <${ email }>"])
  copyFile "wonky-${project.version}-all.jar", '/opt/wonky.jar'

  exposePort 8080
  entryPoint 'java', "-Djava.awt.headless=true", "-Xms128m", "-Xmx128m", '-jar', '/opt/wonky.jar'
}

task buildImage(type: com.bmuschko.gradle.docker.tasks.image.DockerBuildImage) {
  dependsOn createDockerfile
  inputDir = createDockerfile.destFile.get().asFile.parentFile
  dockerFile = createDockerfile.destFile.get().asFile
  //tags = ["domix/wonky:${project.version}-jre-openj9-${jreVersion}".toLowerCase()]
  tags = ["domix/wonky:${project.version}-jre-${jreVersion}".toLowerCase()]
}

task pushImage(type: com.bmuschko.gradle.docker.tasks.image.DockerPushImage) {
  dependsOn buildImage
  def im = buildImage.tags.get().first()
  imageName = im
  def ci = System.getenv('CI') == "true"
  def enabledPush = true
  if(ci && project.version.toString().toLowerCase().endsWith("snapshot")) {
    enabledPush = false
  }
  enabled = enabledPush
  println "Push enabled: ${enabled}"
}

createDockerfile.dependsOn assemble
createDockerfileNative.dependsOn assemble

docker {
  registryCredentials {
    username = (System.getenv('DOCKER_BUILDER_USERNAME') ?: (project.hasProperty('dockerRegistryUsername') ? dockerRegistryUsername : '')) ?: ''
    password = (System.getenv('DOCKER_BUILDER_PASSWORD') ?: (project.hasProperty('dockerRegistryPassword') ? dockerRegistryPassword : '')) ?: ''
  }
}

dependencyUpdates.resolutionStrategy {
  componentSelection { rules ->
    rules.all { ComponentSelection selection ->
      boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
        selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
      }
      if (rejected) {
        selection.reject('Release candidate')
      }
    }
  }
}

license {
  excludes(["**/*.html", "**/*.factories", "**/*.yaml", "**/*.yml", "**/*.xml", "**/*.css", '**/*.png', '**/*.properties'])
  ext.year = Calendar.getInstance().get(Calendar.YEAR)
  ext.name = 'Domingo Suarez Torres'
  ext.email = 'domingo.suarez@gmail.com'

  header rootProject.file('HEADER_LICENSE')
  strictCheck true
}

classes.dependsOn licenseFormat
check.dependsOn licenseFormatTest
jacocoTestReport.dependsOn check
pushImage.dependsOn jacocoTestReport
createReleaseTag.dependsOn pushImage