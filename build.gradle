plugins {
  id("groovy")
  id("jacoco")
  id("com.github.johnrengelman.shadow") version "7.1.2"
  id("io.micronaut.application") version "3.3.2"
  id 'com.github.ben-manes.versions' version '0.51.0'
  id 'com.google.cloud.tools.jib' version '3.4.0'
  id 'net.researchgate.release' version '3.0.2'
}

group = "wonky"

repositories {
  mavenCentral()
  maven {
    url 'https://oss.sonatype.org/content/repositories/snapshots'
  }
}

dependencies {
  annotationProcessor("org.projectlombok:lombok:1.18.32")
  annotationProcessor("io.micronaut:micronaut-http-validation")
  annotationProcessor("io.micronaut.security:micronaut-security-annotations")
  implementation("io.micronaut:micronaut-http-client")
  implementation("io.micronaut:micronaut-jackson-databind")
  implementation("io.micronaut:micronaut-management")
  implementation("io.micronaut:micronaut-runtime")
  implementation("io.micronaut.cache:micronaut-cache-caffeine")
  implementation("io.micronaut.security:micronaut-security")
  implementation("jakarta.annotation:jakarta.annotation-api")
  compileOnly("org.projectlombok:lombok:1.18.32")
  runtimeOnly("ch.qos.logback:logback-classic")
  implementation("io.micronaut:micronaut-validation")


  testImplementation 'org.codehaus.groovy:groovy:3.0.21'
  implementation 'commons-io:commons-io:2.16.1'
  testImplementation 'org.apache.commons:commons-lang3:3.12.0'
}

application {
  mainClass.set("wonky.Application")
}

java {
  sourceCompatibility = JavaVersion.toVersion("21")
  targetCompatibility = JavaVersion.toVersion("21")
}

graalvmNative.toolchainDetection = false

micronaut {
  runtime("netty")
  testRuntime("spock2")
  processing {
    incremental(true)
    annotations("wonky.*")
  }
}

test {
  testLogging {
    events "passed", "skipped", "failed"
    exceptionFormat "full"
  }
  finalizedBy jacocoTestReport
}

jacocoTestReport {
  dependsOn test
  //We are running on Continuos Integration environment
  def ci = System.getenv('CI') == "true"
  reports {
    xml.required = ci
    html.required = !ci
  }
}

jacoco {
  toolVersion = "0.8.11-SNAPSHOT"
}

jib {
  from {
    image = 'amazoncorretto:21-alpine3.18'
  }
  to {
    image = "domix/wonky:${ project.version }".toLowerCase()
    auth {
      username = System.getenv('DOCKER_BUILDER_USERNAME') ?: ''
      password = System.getenv('DOCKER_BUILDER_PASSWORD') ?: ''
    }
  }
  container {
    ports = ['8080']
    volumes = ['/etc/wonky/tenants.yaml']
  }
}

build.dependsOn jibBuildTar
afterReleaseBuild.dependsOn 'jib'
